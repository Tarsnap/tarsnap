# $Id$
# Maintainer: Jelle van der Waa <jelle vd waa.nl>

pkgname=tarsnap
pkgver=1.0.29
pkgrel=1
pkgdesc='An online encrypted snapshotted backup service'
arch=('i686' 'x86_64')
url='http://www.tarsnap.com/'
license=('custom')
depends=('bzip2' 'openssl' 'xz>=5.0.0')
makedepends=('e2fsprogs')
source=("https://www.tarsnap.com/download/$pkgname-autoconf-$pkgver.tgz")
md5sums=('0e5ef12a92fb651b51159b1ad52317f6')
sha256sums=('747510459e4af0ebbb6e267c159aa019f9337d1e07bd9a94f1aa1498081b7598')

build() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  ./configure --prefix=/usr --sysconfdir="/etc/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
